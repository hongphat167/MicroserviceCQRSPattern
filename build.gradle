plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'com.google.protobuf' version '0.9.4' apply false
}

ext {
    // Spring versions
    set('springBootVersion', '3.2.1')
    set('springCloudVersion', '2023.0.0')
    set('openApiVersion', '2.3.0')

    // Other dependencies versions
    set('lombokVersion', '1.18.30')
    set('axonFrameworkVersion', '4.10.3')
    set('postgresqlVersion', 'latest')

    // gRPC versions
    set('grpcVersion', '1.42.0')
    set('protobufVersion', '3.17.3')
    set('grpcSpringBootVersion', '2.13.1.RELEASE')
}

allprojects {
    group = 'com.hongphat'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "io.grpc:grpc-netty-shaded:${grpcVersion}"
            dependency "io.grpc:grpc-protobuf:${grpcVersion}"
            dependency "io.grpc:grpc-stub:${grpcVersion}"
            dependency "com.google.protobuf:protobuf-java:${protobufVersion}"
        }
    }
}