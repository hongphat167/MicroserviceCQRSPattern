plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.google.protobuf' version '0.9.4' apply true
}

ext {
    // Spring versions
    set('springBootVersion', '3.4.0')
    set('springCloudVersion', '2024.0.0')
    set('openApiVersion', '2.3.0')

    // Other dependencies versions
    set('lombokVersion', '1.18.30')
    set('axonFrameworkVersion', '4.9.3')
    set('postgresqlVersion', '42.7.5')

    // gRPC versions
    set('grpcVersion', '1.69.0')
    set('protobufVersion', '3.24.0')
    set('grpcSpringBootVersion', '2.13.1.RELEASE')
}

allprojects {
    group = 'com.hongphat'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://repo.maven.apache.org/maven2/' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://mirrors.huaweicloud.com/repository/maven/' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "io.grpc:grpc-netty-shaded:${grpcVersion}"
            dependency "io.grpc:grpc-protobuf:${grpcVersion}"
            dependency "io.grpc:grpc-stub:${grpcVersion}"
            dependency "com.google.protobuf:protobuf-java:${protobufVersion}"
        }
    }
}